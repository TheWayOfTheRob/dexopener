apply plugin: 'jacoco'

jacoco.toolVersion = versions.jacoco
android.jacoco.version = versions.jacoco

def jacocoReportTask = tasks.create('jacocoTestReport')
androidVariants.all {
    def variantName = name
    def testTaskName = "test${variantName.capitalize()}UnitTest"
    def jacocoTaskName = "jacoco${testTaskName.capitalize()}Report"
    jacocoReportTask.dependsOn tasks.create(
            name: jacocoTaskName,
            type: JacocoReport,
            dependsOn: testTaskName) {
        reports {
            def reportDir = "$buildDir/reports/jacoco/$testTaskName/$variantName"
            html.enabled = true
            html.destination file("$reportDir/html")
            xml.enabled = true
            xml.destination file("$reportDir/${jacocoTaskName}.xml")
        }
        sourceDirectories.from = files(
                android.sourceSets.main.java.srcDirs,
                android.sourceSets[variantName].java.srcDirs,
                android.sourceSets[buildType.name].java.srcDirs,
                productFlavors.collect { android.sourceSets[it.name].java.srcDirs })
        classDirectories.from = fileTree(
                dir: javaCompileProvider.get().destinationDir,
                excludes: ['**/BuildConfig.class', '**/R.class', '**/R$*.class'])
        executionData.from = files("$buildDir/jacoco/${testTaskName}.exec")
    }
}
