import com.android.ddmlib.DdmPreferences

apply plugin: 'com.android.application'

apply from: "$rootDir/gradle/android.gradle"

android {
    defaultConfig {
        applicationId 'com.example.dexopener'
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName version
    }
    buildTypes.debug.testCoverageEnabled true
    DdmPreferences.setTimeOut 60000
    flavorDimensions 'default'
    productFlavors {
        basic {
            dimension 'default'
            testInstrumentationRunner 'com.github.tmurakami.dexopener.DexOpenerAndroidJUnitRunner'
        }
        usingApplicationIdSuffix {
            dimension 'default'
            applicationIdSuffix '.using.application.id.suffix'
            // By default, DexOpener tries loading `Context#getPackageName() + ".BuildConfig"` in
            // order to find the classes to be opened.
            // However, in a project using `applicationIdSuffix`, since the package name of
            // BuildConfig is not equal to the value of `Context#getPackageName()`, loading it will
            // fail.
            // To prevent this, put an AndroidJUnitRunner subclass into the project's instrumented
            // tests directory and call `DexOpener.Builder#buildConfig(Class)` in order to specify
            // the BuildConfig class.
            testInstrumentationRunner 'com.example.dexopener.using.application.id.suffix.JUnitRunner'
        }
    }
}

dependencies {
    androidTestCompile deps.dexopener
    androidTestCompile deps.rules
    androidTestCompile deps.mockito_android
}
