import com.android.ddmlib.DdmPreferences

apply plugin: 'com.android.application'

apply from: "$rootDir/gradle/android.gradle"

android {
    defaultConfig {
        applicationId 'com.example.dexopener'
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName version
    }
    buildTypes.debug.testCoverageEnabled true
    DdmPreferences.setTimeOut 60000
    flavorDimensions 'example'
    productFlavors {
        basic {
            dimension 'example'
            testInstrumentationRunner 'com.github.tmurakami.dexopener.DexOpenerAndroidJUnitRunner'
        }
        usingApplicationIdSuffix {
            dimension 'example'
            applicationIdSuffix '.using.application.id.suffix'
            // DexOpener requests the app's BuildConfig in order to find the classes to be opened.
            // By default, DexOpener will try to load a class named `applicationId + '.BuildConfig'`
            // and therefore loading it will fail if the package name of your app's BuildConfig is
            // different from the `applicationId`. In that case, you should put an
            // AndroidJUnitRunner subclass into your `androidTest` directory and set the BuildConfig
            // class by using `DexOpener.Builder#buildConfig(Class)`.
            testInstrumentationRunner 'com.example.dexopener.using.application.id.suffix.JUnitRunner'
        }
    }
}

dependencies {
    androidTestCompile deps.dexopener
    androidTestCompile deps.rules
    androidTestCompile deps.mockito_android
}
