import com.android.ddmlib.DdmPreferences
import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:5.3.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

apply from: "$rootDir/gradle/android.gradle"

group = 'com.github.tmurakami'

def package_name = "$group.$name"
def repackaged_jar = "$buildDir/libs/repackaged.jar"

android {
    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner 'test.com.github.tmurakami.dexopener.MyAndroidJUnitRunner'
    }
    lintOptions.disable 'InvalidPackage'
    DdmPreferences.setTimeOut 60000
}

configurations {
    jarjar
    repackage
}

dependencies {
    jarjar deps.jarjar
    repackage deps.classinjector_android
    repackage(deps.dexlib2) {
        exclude group: 'com.beust', module: 'jcommander'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    compile deps.runner
    compile files(repackaged_jar) { builtBy 'repackage' }
    testCompile deps.junit
    testCompile deps.mockito
}

task repackage(type: ProGuardTask) {
    outputs.file repackaged_jar
    injars filter: '!META-INF/**', configurations.repackage
    outjars file("$buildDir/tmp/repackage.tmp.jar")
    configuration 'proguard-rules.pro'
    doFirst { libraryjars android.bootClasspath }
    doLast {
        ant {
            taskdef name: 'jarjar',
                    classname: 'com.tonicsystems.jarjar.JarJarTask',
                    classpath: configurations.jarjar.asPath
            jarjar(jarfile: repackaged_jar) {
                outJarFiles.each { zipfileset src: it }
                rule pattern: 'com.**', result: "${package_name}.repackaged.com.@1"
                rule pattern: 'org.**', result: "${package_name}.repackaged.org.@1"
            }
        }
        outJarFiles*.delete()
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
    from android.sourceSets.main.resources.sourceFiles
}

javadoc {
    def missingPackageListDir = "$buildDir/tmp/javadoc/missing-package-list"
    doFirst {
        file(missingPackageListDir).mkdirs()
        file("$missingPackageListDir/package-list").text = 'android.support.test.runner\n'
    }
    options.linksOffline 'https://developer.android.com/reference/', missingPackageListDir
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
