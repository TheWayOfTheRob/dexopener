buildscript {
    ext.versions = [jacoco: '0.8.2',]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.9.3'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.4'
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'pl.allegro.tech.build.axion-release'

scmVersion {
    tag {
        prefix = ''
    }
    hooks {
        pre 'fileUpdate', [file       : 'README.md',
                           pattern    : { v, p -> /([-\/:])$v([-\/'])/ },
                           replacement: { v, p -> "\$1$v\$2" }]
        pre 'commit', { v, p -> "Release version: $v [ci skip]" }
    }
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
        google()
        jcenter()
    }
}

subprojects {
    version = scmVersion.version
    group = 'com.github.tmurakami'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection.all {
        if (candidate.version =~ /(?i)-(?:alpha|beta|rc)[-\d]?/) reject('Release candidate')
    }
}

ext {
    deps = [retrolambda  : 'net.orfjackal.retrolambda:retrolambda:2.5.5',
            jarjar       : 'org.pantsbuild:jarjar:1.6.6',
            classinjector: 'com.github.tmurakami.classinjector:classinjector-android:1.0.0',
            dexlib2      : 'org.smali:dexlib2:2.2.5',
            guava        : 'com.google.guava:guava:18.0',
            junit        : 'junit:junit:4.12',]

    deps.androidx = [annotation: 'androidx.annotation:annotation:1.0.0',]

    def testVersion = '1.1.0'
    deps.androidx.test = [rules : "androidx.test:rules:$testVersion",
                          runner: "androidx.test:runner:$testVersion",]

    def mockitoVersion = '2.23.0'
    deps.mockito = [android: "org.mockito:mockito-android:$mockitoVersion",
                    inline : "org.mockito:mockito-inline:$mockitoVersion",]
}
