buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

apply plugin: 'com.github.ben-manes.versions'

apply from: "$rootDir/gradle/version.gradle"

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
        google()
        jcenter()
    }
}

subprojects {
    version = version_name
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection.all {
        if (candidate.version =~ /(?i)-(?:alpha|beta|rc)[-\d]?/) reject('Release candidate')
    }
}

ext {
    def mockitoVersion = '2.11.0'
    deps = [dexopener      : project(':dexopener'),
            jarjar         : 'com.googlecode.jarjar:jarjar:1.3',
            classinjector  : 'com.github.tmurakami.classinjector:classinjector-android:0.3.0',
            dexlib2        : 'org.smali:dexlib2:2.2.1',
            junit          : 'junit:junit:4.12',
            runner         : 'com.android.support.test:runner:0.5',
            rules          : 'com.android.support.test:rules:1.0.1',
            mockito        : "org.mockito:mockito-inline:$mockitoVersion",
            mockito_android: "org.mockito:mockito-android:$mockitoVersion",]
}
