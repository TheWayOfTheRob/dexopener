buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.9.2'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.3'
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'pl.allegro.tech.build.axion-release'

scmVersion {
    tag {
        prefix = ''
    }
    hooks {
        pre 'fileUpdate', [file       : 'README.md',
                           pattern    : { v, p -> /([-\/:])$v([-\/'])/ },
                           replacement: { v, p -> "\$1$v\$2" }]
        pre 'commit', { v, p -> "Release version: $v [ci skip]" }
    }
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
        google()
        jcenter()
    }
}

subprojects {
    version = scmVersion.version
    group = 'com.github.tmurakami'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection.all {
        if (candidate.version =~ /(?i)-(?:alpha|beta|rc)[-\d]?/) reject('Release candidate')
    }
}

ext {
    deps = [retrolambda  : 'net.orfjackal.retrolambda:retrolambda:2.5.4',
            jarjar       : 'org.pantsbuild:jarjar:1.6.5',
            classinjector: 'com.github.tmurakami.classinjector:classinjector-android:1.0.0',
            dexlib2      : 'org.smali:dexlib2:2.2.4',
            guava        : 'com.google.guava:guava:18.0',
            junit        : 'junit:junit:4.12',]

    deps.android = [annotations: 'com.android.support:support-annotations:27.1.1',]

    def testVersion = '1.0.2'
    deps.android.test = [rules : "com.android.support.test:rules:$testVersion",
                         runner: "com.android.support.test:runner:$testVersion",]

    def mockitoVersion = '2.19.1'
    deps.mockito = [android: "org.mockito:mockito-android:$mockitoVersion",
                    inline : "org.mockito:mockito-inline:$mockitoVersion",]
}
